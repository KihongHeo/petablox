
// name=datarace-nongrded-include-dlog

.number_type A
.number_type H
.number_type I
.number_type M
.number_type E
.number_type L



.decl MI(m:M,i:I) //M0,I0
.input MI()
.decl ME(m:M,e:E) //M0,E0
.input ME()
.decl IM(i:I,m:M) //I0,M0
.input IM()
.decl threadAM(t:A,m:M) //A0,M0
.input threadAM()
.decl transLI(l:L,i:I) //L0,I0
.input transLI()
.decl transLE(l:L,e:E) //L0,E0
.input transLE()
.decl threadStartI(i:I) //I
.input threadStartI()

.decl invkLock(i:I,h:H) //I0,H0
.decl elemLock(e:E,h:H) //E0,H0
.decl syncH(h:H) //H0

.decl syncLH(l:L,h:H) //L0,H0
.input syncLH()

.decl unlockedM(t:A,m:M,h:H) //A0,M0,H0
.decl unlockedI(t:A,i:I,h:H) //A0,I0,H0
.decl unlockedE(t:A,e:E,h:H) //A0,E0,H0
.output unlockedE()


syncH(h) :- syncLH(_,h).

invkLock(i,o) :- transLI(l,i), syncLH(l,o).
elemLock(e,o) :- transLE(l,e), syncLH(l,o).

unlockedM(t,m,o) :- syncH(o), threadAM(t,m).

unlockedI(t,i,o) :- unlockedM(t,m,o), MI(m,i), !invkLock(i,o), !threadStartI(i).
unlockedM(t,n,o) :- unlockedI(t,i,o), IM(i,n).

unlockedE(t,e,o) :- unlockedM(t,m,o), ME(m,e), !elemLock(e,o).

