
// name=pro-downcast-dlog

.number_type H
.number_type M
.number_type T
.number_type V
.number_type C



.decl MobjVarAsgnInst(m:M,l:V,r:V) //M,V,V
.input MobjVarAsgnInst()
.decl reachableCM(c:C,m:M) //C,M
.input reachableCM()
.decl reachableCast(t:T,v:V) //T,V
.decl CVC(c:C,v:V,o:C) //C,V,C
.input CVC()
.decl CH(c:C,h:H) //C,H
.input CH()
.decl sub(s:T,t:T) //T1,T0
.input sub()
.decl HT(h:H,t:T) //H0,T1
.input HT()
.decl McheckCastInst(m:M, v1:V, t:T, v2:V) //M,V,T,V
.input McheckCastInst()
.decl checkExcludedM(m:M) //M
.input checkExcludedM()
.decl ptsVH(v:V,h:H) //V,H
.output ptsVH()
.decl ptsVT(v:V,t:T) //V,T
.output ptsVT()
.decl unsafeDowncast(v:V,t:T) //V,T
.output unsafeDowncast()

reachableCast(t,v2) :- McheckCastInst(m,_,t,v2), reachableCM(_,m), !checkExcludedM(m).

ptsVH(v,h) :- CVC(_,v,o), CH(o,h).
ptsVT(v,t) :- ptsVH(v,h), HT(h,t).

unsafeDowncast(v,t1) :- reachableCast(t1,v), ptsVT(v,t2), !sub(t2,t1).
