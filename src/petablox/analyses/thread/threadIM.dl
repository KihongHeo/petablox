
// name=threadIM-dlog

.number_type M
.number_type V
.number_type H
.number_type Z
.number_type I
.number_type T



.decl IinvkArg(i:I,z:Z,v:V) //I0,Z0,V1
.input IinvkArg()
.decl cha(m:M,t:T,n:M) //M1,T1,M0
.input cha()
.decl VH(v:V,h:H) //V0,H0
.input VH()
.decl HT(h:H,t:T) //H0,T1
.input HT()
.decl threadAllocI(i:I) //I0
.input threadAllocI()
.decl threadStartI(i:I) //I0
.input threadStartI()
.decl excludeThreadStartI(i:I) //I0
.input excludeThreadStartI()

.decl runCHA(h:H,m:M) //H0,M0
.decl threadRunM(m:M) //M0
.decl threadAllocIH0(i:I,h:H) //I0,H0
.decl threadAllocIH1(i:I,h:H) //I0,H0
.decl threadStartIH0(i:I,h:H) //I0,H0

.decl threadIM(i:I,m:M) //I0,M0
.output threadIM()
.decl nonMainThreadM(m:M) //M0
.output nonMainThreadM()


runCHA(h,n) :- cha("<java.lang.Runnable: void run()>",t,n), HT(h,t).

threadRunM("<java.lang.Thread: void run()>").

threadAllocIH0(i,h) :- threadAllocI(i), IinvkArg(i,0,v), VH(v,h).
threadAllocIH1(i,h) :- threadAllocI(i), IinvkArg(i,1,v), VH(v,h).
threadAllocIH1(i,h) :- threadAllocI(i), IinvkArg(i,2,v), VH(v,h).
threadStartIH0(i,h) :- threadStartI(i), IinvkArg(i,0,v), VH(v,h), !excludeThreadStartI(i).

threadIM(i,m) :- threadStartIH0(i,h), runCHA(h,m), !threadRunM(m).
threadIM(i,n) :- threadStartIH0(i,h), runCHA(h,m), threadRunM(m), threadAllocIH0(i2,h), threadAllocIH1(i2,h2), runCHA(h2,n).
nonMainThreadM(m) :- threadIM(_,m).
